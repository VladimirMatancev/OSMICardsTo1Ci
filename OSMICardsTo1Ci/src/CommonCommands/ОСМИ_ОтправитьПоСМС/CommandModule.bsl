
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КартаОСМИ = ОСМИ_ОбщиеСервер.ПолучитьЭлектроннуюКарту(ПараметрКоманды);
	
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьЭлектроннуюКарту", ЭтотОбъект, ПараметрКоманды),
			"Для данной карты 1С не найдено зарегистрированной электронной карты ОСМИ" + Символы.ПС +
			"Зарегистрировать новую электронную карту ОСМИ?", РежимДиалогаВопрос.ДаНет, 60,
			КодВозвратаДиалога.Нет, "Внимание", КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтправкуКартыПоТелефону(ПараметрКоманды, КартаОСМИ);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектроннуюКарту(РезультатВопроса, Карта1С) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КартаОСМИ = СоздатьЭлектроннуюКартуНаСервере(Карта1С);
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьПредупреждение(, "Не удалось создать электронную карту на сервисе ОСМИ", 30, "Ошибка");
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтправкуКартыПоТелефону(Карта1С, КартаОСМИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуКартыПоТелефону(Карта1С, КартаОСМИ)
	
	Телефон = ОСМИ_ОбщиеСервер.ПолучитьТелефонДляКарты1С(Карта1С);
	Если НЕ ЗначениеЗаполнено(Телефон) Тогда
		ПоказатьПредупреждение(, "Не удалось получить номер телефона для отправки СМС", 30, "Ошибка");
		Возврат;
	КонецЕсли;
		
	Результат = ОСМИ_РаботаСAPI.ОтправитьПоСМС(ОСМИ_ОбщиеСервер.ПолучитьСерийныйНомерКартыОСМИ(КартаОСМИ), Телефон);
	Если Результат.Успех Тогда
		ПоказатьПредупреждение(, "Запрос на отправку карты по СМС отправлен", 30, "Информация");
	Иначе
		ПоказатьПредупреждение(, "Ошибка выполнения к сервису ОСМИ для отправки СМС", 30, "Ошибка");
	КонецЕсли;
	
КонецПроцедуры

&наСервере
Функция СоздатьЭлектроннуюКартуНаСервере(Карта1С)
	
	Карта = Справочники.ОСМИ_ЭлектроннаяКарта.СоздатьЭлемент();
	
	Карта.СоздатьКартуИзКарты1С(Карта1С);
	
	Возврат Карта.Ссылка;

КонецФункции