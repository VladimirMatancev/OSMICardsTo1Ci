
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	КартаОСМИ = ОСМИ_ОбщиеСервер.ПолучитьЭлектроннуюКарту(ПараметрКоманды);
	
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьЭлектроннуюКарту", ЭтотОбъект, ПараметрКоманды),
			"Для данной карты 1С не найдено зарегистрированной электронной карты ОСМИ" + Символы.ПС +
			"Зарегистрировать новую электронную карту ОСМИ?", РежимДиалогаВопрос.ДаНет, 60,
			КодВозвратаДиалога.Нет, "Внимание", КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуКартыОСМИ(КартаОСМИ);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектроннуюКарту(РезультатВопроса, Карта1С) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КартаОСМИ = СоздатьЭлектроннуюКартуНаСервере(Карта1С);
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьПредупреждение(, "Не удалось создать электронную карту на сервисе ОСМИ", 30, "Ошибка");
		Возврат;
	Иначе
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(1, "Телефон (СМС)");
		СписокКнопок.Добавить(2, "Электронная почта");
		СписокКнопок.Добавить(0, "Нет");
			
		ПоказатьВопрос(Новый ОписаниеОповещения("СозданиеЭлектроннойКартыЗавершение", ЭтотОбъект,
			Новый Структура("Карта1С, КартаОСМИ", Карта1С, КартаОСМИ)),
			"Электронная карта создана." + Символы.ПС +
			"Отправить электронную карту на телефон (СМС) или по электронной почте?", СписокКнопок, 15,	0, "Внимание", 0);
		Возврат;
		
	КонецЕсли;
	
	ОткрытьФормуКартыОСМИ(КартаОСМИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКартыОСМИ(КартаОСМИ)
	
	ОткрытьФорму("Справочник.ОСМИ_ЭлектроннаяКарта.ФормаОбъекта", Новый Структура("Ключ", КартаОСМИ)); 	
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЭлектроннойКартыЗавершение(ЗначениеОтвета, СтруктураКарт) Экспорт
	
	Если ЗначениеОтвета = 1 Тогда
		
		Телефон = ОСМИ_ОбщиеСервер.ПолучитьТелефонДляКарты1С(СтруктураКарт.Карта1С);
		Если НЕ ЗначениеЗаполнено(Телефон) Тогда
			ПоказатьПредупреждение(, "Не удалось получить номер телефона для отправки СМС", 30, "Ошибка");
			Возврат;
		КонецЕсли;
		
		Результат = ОСМИ_РаботаСAPI.ОтправитьПоСМС(ОСМИ_ОбщиеСервер.ПолучитьСерийныйНомерКартыОСМИ(СтруктураКарт.КартаОСМИ),
			Телефон);
		Если Результат.Успех Тогда
			ПоказатьПредупреждение(, "Запрос на отправку карты по СМС отправлен", 30, "Информация");
		Иначе
			ПоказатьПредупреждение(, "Ошибка выполнения к сервису ОСМИ для отправки СМС", 30, "Ошибка");
		КонецЕсли;
		
	ИначеЕсли ЗначениеОтвета = 2 Тогда
		
		АдресПочты = ОСМИ_ОбщиеСервер.ПолучитьEmailДляКарты1С(СтруктураКарт.Карта1С);
		Если НЕ ЗначениеЗаполнено(АдресПочты) Тогда
			ПоказатьПредупреждение(, "Не удалось получить адрес электронной почты для отправки СМС", 30, "Ошибка");
			Возврат;
		КонецЕсли;
		
		Результат = ОСМИ_РаботаСAPI.ОтправитьПоПочте(ОСМИ_ОбщиеСервер.ПолучитьСерийныйНомерКартыОСМИ(СтруктураКарт.КартаОСМИ),
			АдресПочты);
		Если Результат.Успех Тогда
			ПоказатьПредупреждение(, "Запрос на отправку карты по электронной почте отправлен", 30, "Информация");
		Иначе
			ПоказатьПредупреждение(, "Ошибка выполнения к сервису ОСМИ для отправки карты по электронной почте", 30, "Ошибка");
		КонецЕсли;
		
	КонецЕсли;
	
	ОткрытьФормуКартыОСМИ(СтруктураКарт.КартаОСМИ);
	
КонецПроцедуры

&НаСервере
Функция СоздатьЭлектроннуюКартуНаСервере(Карта1С)
	
	КартаОСМИ = Справочники.ОСМИ_ЭлектроннаяКарта.СоздатьЭлемент();
	КартаОСМИ.СоздатьКартуИзКарты1С(Карта1С);
	Возврат ?(КартаОСМИ.Ссылка.Пустая(), Неопределено, КартаОСМИ.Ссылка);

КонецФункции
