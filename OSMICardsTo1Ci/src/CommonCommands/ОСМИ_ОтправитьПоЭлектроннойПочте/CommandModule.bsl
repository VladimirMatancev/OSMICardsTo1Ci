
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КартаОСМИ = ОСМИ_ОбщиеСервер.ПолучитьЭлектроннуюКарту(ПараметрКоманды);
	
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьЭлектроннуюКарту", ЭтотОбъект, ПараметрКоманды),
			"Для данной карты 1С не найдено зарегистрированной электронной карты ОСМИ" + Символы.ПС +
			"Зарегистрировать новую электронную карту ОСМИ?", РежимДиалогаВопрос.ДаНет, 60,
			КодВозвратаДиалога.Нет, "Внимание", КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтправкуКартыПоПочте(ПараметрКоманды, КартаОСМИ);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектроннуюКарту(РезультатВопроса, Карта1С) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КартаОСМИ = СоздатьЭлектроннуюКартуНаСервере(Карта1С);
	Если НЕ ЗначениеЗаполнено(КартаОСМИ) Тогда
		ПоказатьПредупреждение(, "Не удалось создать электронную карту на сервисе ОСМИ", 30, "Ошибка");
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтправкуКартыПоПочте(Карта1С, КартаОСМИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуКартыПоПочте(Карта1С, КартаОСМИ)
	
	АдресПочты = ОСМИ_ОбщиеСервер.ПолучитьEmailДляКарты1С(Карта1С);
	Если НЕ ЗначениеЗаполнено(АдресПочты) Тогда
		ПоказатьПредупреждение(, "Не удалось получить адрес электронной почты для отправки карты", 30, "Ошибка");
		Возврат;
	КонецЕсли;
		
	Результат = ОСМИ_РаботаСAPI.ОтправитьПоПочте(ОСМИ_ОбщиеСервер.ПолучитьСерийныйНомерКартыОСМИ(КартаОСМИ), АдресПочты);
	Если Результат.Успех Тогда
		ПоказатьПредупреждение(, "Запрос на отправку карты по электронной почте отправлен", 30, "Информация");
	Иначе
		ПоказатьПредупреждение(, "Ошибка выполнения к сервису ОСМИ для отправки карты по электронной почте", 30, "Ошибка");
	КонецЕсли;
	
КонецПроцедуры

&наСервере
Функция СоздатьЭлектроннуюКартуНаСервере(Карта1С)
	
	КартаОСМИ = Справочники.ОСМИ_ЭлектроннаяКарта.СоздатьЭлемент();
	КартаОСМИ.СоздатьКартуИзКарты1С(Карта1С);
	Возврат ?(КартаОСМИ.Ссылка.Пустая(), Неопределено, КартаОСМИ.Ссылка);

КонецФункции