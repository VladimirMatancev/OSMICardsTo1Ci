// Функция - Получить электронную карту
//
// Параметры:
//  ДисконтнаяКарта	 - ОбпределяемыйТип.ОСМИ_ДисконтнаяКарта - Карта 1С
// 
// Возвращаемое значение:
//  СправочникСсылка.ОСМИ_ЭлектроннаяКарта - Электронная карта ОСМИ.
//
Функция ПолучитьЭлектроннуюКарту(ДисконтнаяКарта) Экспорт
	
	Запрос = Новый Запрос; 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОСМИ_СоответствиеКарт.ЭлектроннаяКарта
	|ИЗ
	|	РегистрСведений.ОСМИ_СоответствиеКарт КАК ОСМИ_СоответствиеКарт
	|ГДЕ
	|	ОСМИ_СоответствиеКарт.ОбъектБазы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДисконтнаяКарта);
	Выб = Запрос.Выполнить().Выбрать();
	
	Если выб.Следующий() Тогда 
		Возврат Выб.ЭлектроннаяКарта;
	Иначе
		Возврат Неопределено
	КонецЕсли;
	
КонецФункции
	
// Функция - Получить1 с карту
//
// Параметры:
//  ЭлектроннаяКарта - СправочникСсылка.ОСМИ_ЭлектроннаяКарта - Карта ОСМИ
// 
// Возвращаемое значение:
//  ОбпределяемыйТип.ОСМИ_ДисконтнаяКарт - Ссылка на дисконтную карту системы
//
Функция Получить1СКарту(ЭлектроннаяКарта) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОСМИ_СоответствиеКарт.ОбъектБазы
	|ИЗ
	|	РегистрСведений.ОСМИ_СоответствиеКарт КАК ОСМИ_СоответствиеКарт
	|ГДЕ
	|	ОСМИ_СоответствиеКарт.ЭлектроннаяКарта = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ЭлектроннаяКарта);
	Выб = Запрос.Выполнить().Выбрать();
	
	Если выб.Следующий() Тогда 
		Возврат Выб.ОбъектБазы;
	Иначе
		Возврат Неопределено
	КонецЕсли;
	
КонецФункции
	
// Функция - Проверить соответствие полей шаблонов карт
// 
// Возвращаемое значение:
//  Массив - Массив ошибок в шабонах, хранимых в 1С.
//
Функция ПроверитьСоответствиеПолейШаблоновКарт() Экспорт
	
	СписокШаблонов = ОСМИ_РаботаСAPI.ЗапроситьСписокДоступныхШаблонов();
	Если НЕ СписокШаблонов.Успех Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СписокШаблоновОшибки = Новый Массив;
	
	Для Каждого Шаблон Из СписокШаблонов.Ответ.templates Цикл
		
		ШаблонСпр = Справочники.ОСМИ_ШаблонКарты.НайтиПоНаименованию(Шаблон, Истина);
		Если ШаблонСпр.Пустая() Тогда
			сОшибка = Новый Структура;
			сОшибка.Вставить("ИмяШаблона", Шаблон);
			сОшибка.Вставить("Ошибка", "Шаблон """+Шаблон+""" в 1С не найден");
			СписокШаблоновОшибки.Добавить(сОшибка);
		КонецЕсли;
			
		ДанныеШаблона = ОСМИ_РаботаСAPI.ЗапроситьИнформациюОШаблоне(Шаблон);
		Если НЕ ДанныеШаблона.Успех Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		МассивПолей = ДанныеШаблона.Ответ.values;
		МассивПолей1С = ШаблонСпр.ПоляШаблона;
		сПоиск = Новый Структура("Имя");
		ЕстьОшибки = Ложь;
		Для Каждого Поле Из МассивПолей Цикл
			
			сПоиск.Имя = Поле.label;
			
			Найдено = МассивПолей1С.НайтиСтроки(сПоиск);
			Если НЕ Найдено.Количество() = 1 Тогда
				сОшибка = Новый Структура;
				сОшибка.Вставить("ИмяШаблона", Шаблон);
				сОшибка.Вставить("Ошибка", "В шаблоне """+Шаблон+""" имеется рассогласование полей");
				СписокШаблоновОшибки.Добавить(сОшибка);
				ЕстьОшибки = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Поле1С Из МассивПолей1С Цикл
			
			ПолеЕсть = Ложь;
			
			Для Каждого Поле Из МассивПолей Цикл
				Если Поле1С.Имя = Поле.label Тогда
					ПолеЕсть = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ПолеЕсть Тогда
				сОшибка = Новый Структура;
				сОшибка.Вставить("ИмяШаблона", Шаблон);
				сОшибка.Вставить("Ошибка", "В шаблоне """+Шаблон+""" имеется рассогласование полей");
				СписокШаблоновОшибки.Добавить(сОшибка);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СписокШаблоновОшибки;			   
	
КонецФункции

Функция УТФвСтроку(ВхСтр) экспорт
    ВыхСтр="";      
    поз=1; 
    Пока поз<=СтрДлина(ВхСтр) Цикл
        симв=Сред(ВхСтр,поз,1);
        Если симв="\" И Сред(ВхСтр,поз+1,1)="u" Тогда
           поз=поз+2;
           Вес=4096;
           ВыхКод=0;
           Для п=0 По 3 Цикл
               кодСимв=КодСимвола(ВхСтр,поз+п);
               Если кодСимв>96 Тогда // a-f
                  кодСимв=кодСимв-87;
               ИначеЕсли кодСимв>64 Тогда // A-F
                  кодСимв=кодСимв-55;
               Иначе
                  кодСимв=кодСимв-48; // 0-9
              КонецЕсли;
              ВыхКод=ВыхКод+кодСимв*Вес;
              Вес=Вес/16;
          КонецЦикла;
          ВыхСтр=ВыхСтр+Символ(ВыхКод);
          поз=поз+4;
        Иначе
           ВыхСтр=ВыхСтр+симв;
           поз=поз+1;
        КонецЕсли;
    КонецЦикла;
    Возврат ВыхСтр;
КонецФункции

Функция RegEx(Строка, Фасет) экспорт
	// http://infostart.ru/public/464971/
    Чтение = Новый ЧтениеXML;
    Чтение.УстановитьСтроку(
                "<Model xmlns=""http://v8.1c.ru/8.1/xdto"" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" xsi:type=""Model"">
                |<package targetNamespace=""sample-my-package"">
                |<valueType name=""testtypes"" base=""xs:string"">
                |<pattern>" + Фасет + "</pattern>
                |</valueType>
                |<objectType name=""TestObj"">
                |<property xmlns:d4p1=""sample-my-package"" name=""TestItem"" type=""d4p1:testtypes""/>
                |</objectType>
                |</package>
                |</Model>");

    Модель = ФабрикаXDTO.ПрочитатьXML(Чтение);
    МояФабрикаXDTO = Новый ФабрикаXDTO(Модель);
    Пакет = МояФабрикаXDTO.Пакеты.Получить("sample-my-package");
    Тест = МояФабрикаXDTO.Создать(Пакет.Получить("TestObj"));

    Попытка
        Тест.TestItem = Строка;
        Возврат Истина
    Исключение
        Возврат Ложь
    КонецПопытки;
    
КонецФункции

Функция ФункцияВосстановления(Значение) Экспорт
	Если RegEx(Значение,"[0-9]{4}-[0-9]{2}-[0-9]{2}T[0-9]{2}:[0-9]{2}:[0-9]{2}[Z]") тогда		
		Возврат                    
		МестноеВремя(
		Дата(
		СтрЗаменить(
			СтрЗаменить(
				СтрЗаменить(
					СтрЗаменить(
						СтрЗаменить(
							Значение," ",""
									)
								,":",""
								)
							,"-",""
							) 
						,"T",""
						)
					,"Z",""
					)
				)
			);
	ИначеЕсли Найти(Значение,"\u") > 0 Тогда
		Возврат УТФвСтроку(Значение);
	КонецЕсли;
	Возврат Значение		
КонецФункции

Функция ПолучитьТелефонДляКарты1С(Карта1С) Экспорт
	
	Возврат Справочники.ОСМИ_ИсточникДанных.Телефон.ПолучитьОбъект().ПолучитьЗначениеПоля(Карта1С);
	
КонецФункции

Функция ПолучитьEmailДляКарты1С(Карта1С) Экспорт
	
	Возврат Справочники.ОСМИ_ИсточникДанных.ЭлектроннаяПочта.ПолучитьОбъект().ПолучитьЗначениеПоля(Карта1С);
	
КонецФункции

Функция ПолучитьСерийныйНомерКартыОСМИ(КартаОСМИ) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ОСМИ_ЭлектроннаяКарта.Код КАК Код
	                      |ИЗ
	                      |	Справочник.ОСМИ_ЭлектроннаяКарта КАК ОСМИ_ЭлектроннаяКарта
	                      |ГДЕ
	                      |	ОСМИ_ЭлектроннаяКарта.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", КартаОСМИ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Код;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции