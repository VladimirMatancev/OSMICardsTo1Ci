
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРеквизитовКарты();
	
	Элементы.Таблица.ОтображатьЗаголовки = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьСписокРеквизитовКарты()
	
	ТипУКарты = Метаданные.ОпределяемыеТипы.ОСМИ_ДисконтнаяКарта.Тип.Типы()[0];
	
	СпрКарты = Метаданные.НайтиПоТипу(ТипУКарты);
	ИмяСпрКарты = СпрКарты.Имя;
	
	Если СпрКарты = Неопределено Тогда
		Сообщить("Не определены метаданные карт");
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Новый Массив;
	
	Для Каждого рекв Из СпрКарты.Реквизиты Цикл
		
		Если НЕ Справочники.ТипВсеСсылки().СодержитТип(рекв.Тип.Типы()[0]) Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(рекв.Имя);
		
	КонецЦикла;	
			
	Элементы.РеквизитКарты.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчет(Команда)
	
    ДополнительныеПараметры = Новый Структура;
    ДополнительныеПараметры.Вставить("ВыборЗавершение", Новый ОписаниеОповещения("ВложениеВыборЗавершение", ЭтотОбъект));
    Оповещение = Новый ОписаниеОповещения("НачатьПодключениеРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
    НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);	
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПодключениеРасширенияРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
    
    Если Не Подключено Тогда
        Оповещение = Новый ОписаниеОповещения("НачатьУстановкуРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
        ТекстСообщения = НСтр("ru='Для продолжении работы необходимо установить расширение для веб-клиента ""1С:Предприятие"". Установить?'");
        ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет); 
	Иначе
        ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ВыборЗавершение, Истина);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура НачатьУстановкуРасширенияРаботыСФайламиЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат = КодВозвратаДиалога.Да Тогда
        НачатьУстановкуРасширенияРаботыСФайлами(ДополнительныеПараметры.ВыборЗавершение);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ВложениеВыборЗавершение(ДополнительныеПараметры, Результат) Экспорт
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор		 = Ложь;
	ВыборФайла.Заголовок                 = "Выберите файлы с изображениями";
	ВыборФайла.Фильтр                    = "Книга Excel (*.xls, *.xlsx)| *.xls; *.xlsx";
    
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	НачатьПомещениеФайлов(ОписаниеОповещения, , ВыборФайла, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
    Если ПомещенныеФайлы = Неопределено Тогда
        Возврат;
    КонецЕсли;

    ПереданныйФайл = ПомещенныеФайлы[0];
	ИмяФайла = Новый Файл(ПереданныйФайл.Имя);
	РасширениеФайла = ИмяФайла.Расширение;
	АдресФайла = ПереданныйФайл.Хранение;
	
	ЗагрузитьОтчетНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьОтчетНаСервере()
	
	Если АдресФайла = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДД = ПолучитьИзВременногоХранилища(АдресФайла);
	
	ТемпФайл = ПолучитьИмяВременногоФайла(РасширениеФайла);
	
	ДД.Записать(ТемпФайл);
	
	Таблица.Прочитать(ТемпФайл);

КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница1 Тогда
		
		Если ПроверитьЗаполненностьПолей() Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница2;
		Элементы.Далее.Заголовок = "Перенести в документ";
		Элементы.РезультатРеквизит.Заголовок = РеквизитКартыДляПоиска;
		Элементы.Группа6.Видимость = Истина;
		
		ЗаполнитьРеквизитыКарты();
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница2 Тогда
		
		ЗагрузитьДанныеВДокумент(ЭтаФорма.ВладелецФормы.УникальныйИдентификатор);
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыКарты()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Реквизит", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
	
	КолСтрок = Таблица.ВысотаТаблицы;
	
	Для Сч = НомерСтроки По КолСтрок Цикл
		
		Область = Таблица.Область("R" + Строка(Сч) + "C" + Строка(НомерКолонки));
		
		Рекв = "";
		Если Область.СодержитЗначение Тогда
			Рекв = Область.Значение;
		Иначе
			Рекв = Область.Текст;
		КонецЕсли;
		
		Если Рекв = "" Тогда
			Продолжить;
		КонецЕсли;
		
		НовСтр = ТЗ.Добавить();
		НовСтр.Реквизит = Рекв;
		
	КонецЦикла;
	
	////////////////////////////////////////////////////////////////////////////////////
	МетРеквизита = Метаданные.Справочники[ИмяСпрКарты].Реквизиты[РеквизитКартыДляПоиска];
	ТипРеквизита = МетРеквизита.Тип.Типы()[0];
	СпрРеквизит = Метаданные.НайтиПоТипу(ТипРеквизита);
	
	Если СпрРеквизит = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗ.Реквизит
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	&ТЗ КАК ТЗ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Реквизит КАК Данные,
	               |	ЕСТЬNULL(Владелец.Ссылка, НЕОПРЕДЕЛЕНО) КАК Реквизит,
	               |	ЕСТЬNULL(Карта.Ссылка, НЕОПРЕДЕЛЕНО) КАК Карта
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.#1 КАК Владелец
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.#2 КАК Карта
	               |			ПО Владелец.Ссылка = Карта.#3
	               |		ПО ВТ.Реквизит = Владелец.Наименование";
				   
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#1", СпрРеквизит.Имя);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#2", ИмяСпрКарты);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#3", РеквизитКартыДляПоиска);
	
	Результат.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

Процедура ЗагрузитьДанныеВДокумент(ИД)
	
	Карты = Результат.Выгрузить();
	АдресВХранилище = ПоместитьВоВременноеХранилище(Карты, ИД);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполненностьПолей()
	
	Ответ = Ложь;
	
	Если РеквизитКартыДляПоиска = "" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана реквизит для поиска";
		Сообщение.Поле = "РеквизитКартыДляПоиска";
		Сообщение.Сообщить();
		Ответ = Истина;
	КонецЕсли;
	
	Если НомерКолонки = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана колонка";
		Сообщение.Поле = "НомерКолонки";
		Сообщение.Сообщить();
		Ответ = Истина;
	КонецЕсли;
	
	Если НомерСтроки = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана строка";
		Сообщение.Поле = "НомерСтроки";
		Сообщение.Сообщить();
		Ответ = Истина;
	КонецЕсли;
	
	Если Таблица.ВысотаТаблицы = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не загружены данные";
		Сообщение.Поле = "Таблица";
		Сообщение.Сообщить();
		Ответ = Истина;
	КонецЕсли;
		
	Возврат Ответ;
		
КонецФункции

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница1;
	Элементы.Далее.Заголовок = "Далее >";
	Элементы.Группа6.Видимость = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если АдресВХранилище <> "" Тогда
		Структура = Новый Структура("АдресВХранилище", АдресВХранилище);
		ОповеститьОВыборе(Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	АдресВХранилище = "";
	Закрыть();
	
КонецПроцедуры

