
&НаКлиенте
Процедура ПерезаполнитьПоля(Команда)
	ПерезаполнитьПоляНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоляНаСервере()
	
	тчПоля = Объект.ПоляШаблона;
	тчПоля.Очистить();
	
	ЭтаФорма.Модифицированность = Истина;
	
	ДанныеШаблона = ОСМИ_РаботаСAPI.ЗапроситьИнформациюОШаблоне(Объект.Наименование);
	Если НЕ ДанныеШаблона.Успех Тогда
		Сообщить("Не удалось получить данные шаблона с сервера ОСМИ");
		Возврат;
	КонецЕсли;

	Для Каждого Поле Из ДанныеШаблона.Ответ.values Цикл
		НовПоле = тчПоля.Добавить();
		НовПоле.Имя = Поле.label;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СодержимоеБаркодаПриИзменении(Элемент)
		
	Если Объект.СодержимоеБаркода <> ПредопределенноеЗначение("Перечисление.ОСМИ_СодержимоеБаркодаПодписи.Текст") Тогда
		Объект.СпособПолученияСодержимогоБаркода = ПредопределенноеЗначение("Справочник.ОСМИ_ИсточникДанных.ПустаяСсылка");
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СодержимоеПодписиПриИзменении(Элемент)
	
	Если Объект.СодержимоеПодписи <> ПредопределенноеЗначение("Перечисление.ОСМИ_СодержимоеБаркодаПодписи.Текст") Тогда
		Объект.СпособПолученияСодержимогоПодписи = ПредопределенноеЗначение("Справочник.ОСМИ_ИсточникДанных.ПустаяСсылка");
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.СпособПолученияСодержимогоБаркода.Видимость = 
		Объект.СодержимоеБаркода = ПредопределенноеЗначение("Перечисление.ОСМИ_СодержимоеБаркодаПодписи.Текст");
	Элементы.СпособПолученияСодержимогоПодписи.Видимость = 
		Объект.СодержимоеПодписи = ПредопределенноеЗначение("Перечисление.ОСМИ_СодержимоеБаркодаПодписи.Текст");
	
КонецПроцедуры
